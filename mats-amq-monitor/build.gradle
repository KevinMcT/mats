apply plugin: 'spring-boot'

jar {
    baseName = 'mats-amq-monitor'
    version = '0.0.1-SNAPSHOT'
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
    amqWebConsoleWar
}

// :: Get hold of the ActiveMQ Web Console WAR file, and place it in src/main/resources before compileJava
ext.activeMqWebConsoleResource = file("src/main/resources/activemq-web-console.war")
task downloadAmqWebConsoleWar {
    inputs.files configurations.amqWebConsoleWar
    outputs.file activeMqWebConsoleResource
    doLast {
        configurations.amqWebConsoleWar.resolve().each { warFile ->
            println "Got ActiveMQ Web Console WAR: $warFile"
            println " \\- Copying to destination: ${activeMqWebConsoleResource.getAbsolutePath()}"
            activeMqWebConsoleResource << warFile.getBytes()
        }
    }
}
compileJava.dependsOn downloadAmqWebConsoleWar

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-remote-shell')
    compile('com.vaadin:vaadin-spring-boot-starter:1.0.0')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jetty')
    // https://mvnrepository.com/artifact/org.eclipse.jetty/apache-jsp
    compile group: 'org.eclipse.jetty', name: 'apache-jsp', version: '9.3.11.v20160721'

    // https://mvnrepository.com/artifact/javax.servlet/jstl
    compile group: 'javax.servlet', name: 'jstl', version: '1.2'

    amqWebConsoleWar("org.apache.activemq:activemq-web-console:${activeMqVersion}@war")

    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement { imports { mavenBom "com.vaadin:vaadin-bom:7.6.3" } }
